#+TITLE: Files.

We do a lot with files. Mainly, we want to serve them with a [[file:httpd/file-handler.org][file-handler]].


* Usage

There are a few things that can help with the serving of files.

#+begin_src scheme
(import :drewc/ftw/file)
(file-headers "/etc/passwd")

;; =>
;; (("Last-Modified" . "Wed, 30 Jun 2021 00:35:43 GMT")
;;  ("Content-Length" . 2458)
;;  ("Content-Type" . "text/plain; charset=us-ascii")
;;  ("Content-Disposition" . "attachment; filename=passwd")
;;  ("Accept-Ranges" . "none"))
#+end_src

#+BEGIN_SRC scheme :padline no :tangle "../ftw/file.ss"
(export #t file-size file-exists?)
(import
  :drewc/ftw/file/mime-type :drewc/ftw/timestamp :std/format :std/sugar
  (only-in :gerbil/gambit/os
           file-info-last-modification-time
           file-info file-size file-exists?)
  :std/pregexp :std/srfi/1 :std/srfi/13 :std/misc/process)

(def (file-extension pathname)
  (path-extension pathname))

(def (file-mime-type pathname)
  (string-drop-right (run-process ["file" "--mime" "--brief" pathname]) 1))

(def (file-content-type pathname)
  (let* ((ext (file-extension pathname))
         (mtype (if (equal? ext "") #f (extension->mime-type ext)))
         (type #f))

    ;; If it is text or non-existant, use `file --mime --brief` to guess the encoding
    (when (or (not mtype)
              (and (pregexp-match "^text" mtype)
               (not (pregexp-match ";\\s*charset=" mtype))))
      (set! type (try (file-mime-type pathname)
                      (catch (e) #f))))
    (or type mtype "application/octet-stream")))

(def (file-modification-rfc-1123-date pathname)
  (rfc-1123-date<-gambit/os-time
   (file-info-last-modification-time (file-info pathname))))

(def (file-name pathname)
  (path-strip-directory pathname))

(def (file-headers (file "/bin/sh"))
  [["Last-Modified" (file-modification-rfc-1123-date file) ...]
   ["Content-Length" (number->string (file-size file)) ...]
   ["Content-Type" (file-content-type file) ...]
   ["Content-Disposition" (format "attachment; filename=~a" (file-name file)) ...]
   ["Accept-Ranges" . "none"]])
#+END_SRC
